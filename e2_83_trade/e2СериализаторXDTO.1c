Перем ОсобыеСущности;
Перем NS;
Перем rootElement;

//---------------------------------------------------------------------------------------------

Функция СоздатьОтветСписок() Экспорт
	response = СоздатьОбъектXDTO("listResponse");
	rootElement = createSystemResponse(response);
	Возврат response;
КонецФункции

Функция СоздатьОтветЭлементы() Экспорт
	response = СоздатьОбъектXDTO("elementResponse");
	rootElement = createSystemResponse(response);
	Возврат response;
КонецФункции

Функция createSystemResponse(response)
	systemResponse = СоздатьОбъектXDTO("systemResponse");
	systemResponse.systemUid  = ОсобыеСущности.ПолучитьУидСистемы();
	systemResponse.systemName = ОсобыеСущности.ПолучитьИмяСистемы();

	response.systemResponse.Добавить(systemResponse);

	Возврат systemResponse;
КонецФункции
//---------------------------------------------------------------------------------------------

Функция СоздатьОбъектXDTO(typeName) Экспорт
	Возврат ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(NS, typeName));
КонецФункции

//---------------------------------------------------------------------------------------------

Функция СоздатьСущность(ИмяСущности) Экспорт
	entity = СоздатьОбъектXDTO("entity");
	entity.entityName = ИмяСущности;
	rootElement.entity.Добавить(entity);
	Возврат entity;
КонецФункции

Функция СоздатьЭлемент(Сущность, УИДЭлемента) Экспорт
	element = СоздатьОбъектXDTO("element");
	element.elementUid = УИДЭлемента;
	Сущность.element.Добавить(element);
	Возврат element;
КонецФункции

Функция СоздатьТабличнуюЧасть(Элемент, ИмяТабличнойЧасти) Экспорт
	table = СоздатьОбъектXDTO("table");
	table.tableName = ИмяТабличнойЧасти;
	Элемент.table.Добавить(table);
	Возврат table;
КонецФункции

Функция СоздатьСтроку(ТабличнаяЧасть) Экспорт
	row = СоздатьОбъектXDTO("row");
	ТабличнаяЧасть.row.Добавить(row);
	Возврат row;
КонецФункции

Процедура СоздатьРеквизит(Элемент, ИмяРеквизита, Значение) Экспорт
	attribute = СоздатьОбъектXDTO("attribute");
	attribute.attributeName = ИмяРеквизита;
	attribute.value         = Значение;
	Элемент.attribute.Добавить(attribute);
КонецПроцедуры

Процедура СоздатьРеквизитСсылка(Элемент, ИмяРеквизита, ИмяСущности, УИДЭлемента) Экспорт
	reference = СоздатьОбъектXDTO("reference");
	reference.entityName = ИмяСущности;
	reference.elementUid = УИДЭлемента;

	attribute = СоздатьОбъектXDTO("attribute");
	attribute.attributeName = ИмяРеквизита;
	attribute.reference = reference;

	Элемент.attribute.Добавить(attribute);
КонецПроцедуры

Процедура УстановитьПризнакИзменения(Элемент) Экспорт
	Элемент.changed = Истина;
КонецПроцедуры

//---------------------------------------------------------------------------------------------

Функция Инициализировать(_ОсобыеСущности) Экспорт
	ОсобыеСущности = _ОсобыеСущности;
	NS = ОсобыеСущности.ПолучитьПространствоИмен();
	Возврат ЭтотОбъект;
КонецФункции