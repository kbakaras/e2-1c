Перем ОсобыеСущности;
Перем NS;
Перем PREFIX;

Перем request;
Перем context;

//---------------------------------------------------------------------------------------------

Функция createElement(node, name)
	NODE_ELEMENT = 1;
	Возврат ?(node.ownerDocument <> Неопределено, node.ownerDocument, node)
			.createNode(NODE_ELEMENT, PREFIX + ":" + name, NS);
КонецФункции

Функция addElement(node, name)
	element = createElement(node, name);
	node.appendChild(element);
	Возврат element;
КонецФункции

Функция createXML(rootName)
	doc = Новый COMОбъект("MSXML2.DOMDocument");
	doc.appendChild(doc.createProcessingInstruction("xml", "version='1.0' encoding='utf-8'"));
	doc.async = 0;
	doc.setProperty("SelectionLanguage", "XPath");
	doc.setProperty("SelectionNamespaces", "xmlns:" + PREFIX + "='" + NS + "'");

	Возврат addElement(doc, rootName);
КонецФункции

//=============================================================================================

// ЗапрашиваемыеСистемы (Необязательный) - Массив с идентификаторами систем, в которые будет направлен запрос.
// Если не указан, запрос будет направлен во все доступные системы.
Процедура СоздатьЗапросСписка(ЗапрашиваемыеСистемы = Неопределено) Экспорт
	request = createXML("listRequest");
	request.setAttribute("systemUid",  ОсобыеСущности.ПолучитьУидСистемы());
	request.setAttribute("systemName", ОсобыеСущности.ПолучитьИмяСистемы());

	Если ЗапрашиваемыеСистемы <> Неопределено Тогда
		Для Каждого УИДСистемы Из ЗапрашиваемыеСистемы Цикл
			ДобавитьЗапрашиваемуюСистему(УИДСистемы);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура СоздатьЗапросЭлементов(ЗапрашиваемыеСистемы = Неопределено) Экспорт
	request = createXML("elementRequest");
	request.setAttribute("systemUid",  ОсобыеСущности.ПолучитьУидСистемы());
	request.setAttribute("systemName", ОсобыеСущности.ПолучитьИмяСистемы());
	
	Если ЗапрашиваемыеСистемы <> Неопределено Тогда
		Для Каждого УИДСистемы Из ЗапрашиваемыеСистемы Цикл
			ДобавитьЗапрашиваемуюСистему(УИДСистемы);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура СоздатьЗапросОбновления() Экспорт
	УидСистемы = ОсобыеСущности.ПолучитьУидСистемы();
	Если Не ЗначениеЗаполнено(УидСистемы) Тогда
		ВызватьИсключение "Не удалось определить экземпляр ИБ по строке подключения: " + СтрокаСоединенияИнформационнойБазы();
	КонецЕсли;

	request = createXML("updateRequest");
	request.setAttribute("systemUid",  УидСистемы);
	request.setAttribute("systemName", ОсобыеСущности.ПолучитьИмяСистемы());

	context = request;
КонецПроцедуры

Процедура ДобавитьЗапрашиваемуюСистему(УидСистемы) Экспорт
	system = addElement(request, "system");
	system.setAttribute("systemUid", УидСистемы);
КонецПроцедуры

Функция ДобавитьЗапросСущности(ИмяСущности) Экспорт
	entity = addElement(request, "entity");
	entity.setAttribute("entityName", ИмяСущности);
	Возврат entity;
КонецФункции

Функция ДобавитьФильтр(ЗапросСущности, ИмяРеквизита, ВидСравнения) Экспорт
	filter = addElement(ЗапросСущности, "filter");
	filter.setAttribute("attributeName", ИмяРеквизита);
	filter.setAttribute("condition",     ВидСравнения);
	Возврат filter;
КонецФункции

Процедура ДобавитьЗапросЭлемента(ИмяСущности, УидЭлемента) Экспорт
	reference = addElement(request, "reference");
	reference.setAttribute("entityName", ИмяСущности);
	reference.setAttribute("elementUid", УидЭлемента);
КонецПроцедуры

//---------------------------------------------------------------------------------------------

Функция ВыполнитьЗапрос() Экспорт
	Если context <> Неопределено Тогда
		Если request.selectSingleNode("agr:context") = Неопределено Тогда
			request.appendChild(context);
		КонецЕсли;
	КонецЕсли;
	
	http = Новый COMОбъект("MSXML2.XMLHTTP");
	http.open("POST", ОсобыеСущности.ПолучитьСтрокуСоединенияAgr(), 0);
	http.setRequestHeader("Content-Type", "text/xml");
	http.send(request.ownerDocument);

	http.responseXML.setProperty("SelectionLanguage", "XPath");
	http.responseXML.setProperty("SelectionNamespaces", "xmlns:" + PREFIX + "='" + NS + "'");

	Возврат http.responseXML.documentElement;
КонецФункции

Функция ТекстСообщения() Экспорт
	Возврат request.ownerDocument.xml;
КонецФункции

Функция ОтправитьСообщение(ТекстСообщения) Экспорт
	http = Новый COMОбъект("MSXML2.XMLHTTP");
	http.open("POST", ОсобыеСущности.ПолучитьСтрокуСоединенияAgr(), 0);
	http.setRequestHeader("Content-Type", "text/xml");
	http.send(ТекстСообщения);
КонецФункции

Функция ОтправитьСообщениеСПроверкойСистемы(ТекстСообщения) Экспорт
	Если ОсобыеСущности.ПолучитьПризнакПропускаПроверкиСистемы() Тогда
		Возврат ОтправитьСообщение(ТекстСообщения);

	Иначе
		doc = Новый COMОбъект("MSXML2.DOMDocument");
		doc.async = Ложь;
		doc.loadXML(ТекстСообщения);
		root = doc.documentElement;
		systemUid = root.getAttribute("systemUid");

		Если systemUid = ОсобыеСущности.ПолучитьУидСистемы() Тогда
			Возврат ОтправитьСообщение(ТекстСообщения);
			
		Иначе
			root.setAttribute("systemUid",  ОсобыеСущности.ПолучитьУидСистемы());
			root.setAttribute("systemName", ОсобыеСущности.ПолучитьИмяСистемы());
			Возврат ОтправитьСообщение(doc.xml);
		КонецЕсли;
	КонецЕсли;
КонецФункции

//=============================================================================================

Функция СоздатьСущность(ИмяСущности) Экспорт
	Если context = Неопределено Тогда
		context = addElement(request, "context");
	КонецЕсли;

	entity = addElement(context, "entity");
	entity.setAttribute("entityName", ИмяСущности);

	Возврат entity;
КонецФункции

Функция СоздатьЭлемент(Сущность, УИДЭлемента) Экспорт
	element = addElement(Сущность, "element");
	element.setAttribute("elementUid", УИДЭлемента);
	
	Возврат element;
КонецФункции

Функция СоздатьТабличнуюЧасть(Элемент, ИмяТабличнойЧасти) Экспорт
	table = addElement(Элемент, "table");
	table.setAttribute("tableName", ИмяТабличнойЧасти);

	Возврат table;
КонецФункции

Функция СоздатьСтроку(ТабличнаяЧасть) Экспорт
	Возврат addElement(ТабличнаяЧасть, "row");
КонецФункции


Процедура СоздатьРеквизит(Элемент, ИмяРеквизита, Значение) Экспорт
	Реквизит = addElement(Элемент, "attribute");
	Реквизит.setAttribute("attributeName", ИмяРеквизита);
	УстановитьЗначение(Реквизит, Значение);
КонецПроцедуры

Процедура СоздатьРеквизитСсылка(Элемент, ИмяРеквизита, ИмяСущности, УИДЭлемента) Экспорт
	Реквизит = addElement(Элемент, "attribute");
	Реквизит.setAttribute("attributeName", ИмяРеквизита);
	УстановитьСсылку(Реквизит, ИмяСущности, УИДЭлемента);
КонецПроцедуры

Процедура УстановитьПризнакИзменения(Элемент) Экспорт
	Элемент.setAttribute("changed", "true");
КонецПроцедуры


Процедура УстановитьЗначение(Реквизит, Значение) Экспорт
	addElement(Реквизит, "value").text = XMLСтрока(Значение);
КонецПроцедуры

Процедура УстановитьСсылку(Реквизит, ИмяСущности, УИДЭлемента) Экспорт
	reference = addElement(Реквизит, "reference");
	reference.setAttribute("entityName", ИмяСущности);
	reference.setAttribute("elementUid", УИДЭлемента);
КонецПроцедуры

//---------------------------------------------------------------------------------------------

Функция Инициализировать(_ОсобыеСущности) Экспорт
	ОсобыеСущности = _ОсобыеСущности;
	NS = ОсобыеСущности.ПолучитьПространствоИмен();
	PREFIX = "agr";
	Возврат ЭтотОбъект;
КонецФункции